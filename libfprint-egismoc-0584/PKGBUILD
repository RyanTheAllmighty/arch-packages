# Maintainer: Ryan Dowling <ryan@ryandowling.me>

## links
# https://fprint.freedesktop.org
# https://gitlab.freedesktop.org/libfprint/libfprint
# PKGBUILD based on https://aur.archlinux.org/packages/libfprint-git

## basic info
_pkgname="libfprint"
pkgname="$_pkgname-egismoc-0584-git"
pkgver=1.94.8.r1.g8ce356f
pkgrel=1
pkgdesc="Library for fingerprint readers"
url="https://gitlab.freedesktop.org/libfprint/libfprint"
license=('LGPL-2.1-or-later')
arch=('x86_64')

depends=(
  libgudev
  libgusb
  nss
  pixman
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
checkdepends=(
  python
  python-cairo
  python-gobject
  umockdev
)

provides=(
  "$_pkgname=$pkgver"
  'libfprint-2.so'
)
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git" "changes.patch")
sha256sums=('SKIP' 'a71c55dabe91ab38acc6e71061bab314d6705cb7a0395a872dddbc7e6dd7d7cf')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' --exclude='*_*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd "$_pkgsrc"
    patch -Np1 -i ../changes.patch
}

build() {
  local _meson_opts=(
    -Ddrivers=all
    -Dinstalled-tests=false
  )

  arch-meson "$_pkgsrc" build "${_meson_opts[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

